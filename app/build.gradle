buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        mirror {
        }
    }
    compileSdkVersion 22
    buildToolsVersion "23.0.1"
    defaultConfig {
        applicationId "com.morristaedt.mirror"
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 4
        versionName "1.3"
    }
    buildTypes {
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.mirror
        }
    }
}

repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.ahorn:android-rss:cf40b89'
    compile 'com.google.android.gms:play-services-vision:8.1.0'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
}

def signingFile = rootProject.file('signing.properties')
if (signingFile.exists()) {
    def props = new Properties()
    props.load(new FileInputStream(signingFile))
    android.signingConfigs.mirror.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.mirror.storePassword props.keyStorePassword
    android.signingConfigs.mirror.keyAlias props.keyAlias
    android.signingConfigs.mirror.keyPassword props.keyAliasPassword
}
